load("@npm//rollup:index.bzl", "rollup")
load("@npm//jest:index.bzl", "jest")
load("@npm//@storybook/react:index.bzl", "start_storybook")
load("@build_bazel_rules_nodejs//:index.bzl", "copy_to_bin", "js_library", "pkg_npm")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("//rules/test:jest.bzl", "jest_test")
load("//rules/lint:index.bzl", "eslint_config", "eslint_test", "prettier_test")
load("//rules/package_json_helpers:generate.bzl", "generate_package_json_helpers")
load("//rules/package_json_helpers:module_mappings.bzl", "module_mappings")
load("//rules/npm:is_published.bzl", "is_published")
load("//rules/npm:package_info.bzl", "package_info")
load("//rules/npm:append_deps.bzl", "append_deps")

_TESTS = [
    "src/**/*.test.js*",
    "src/**/*.test.ts*",
    "src/**/*.spec.js*",
    "src/**/*.spec.ts*",
    "src/**/__tests__/**/*.js*",
    "src/**/__tests__/**/*.ts*",
    "src/__mocks__/*",
    "src/**/__mocks__/*",
    "src/utils/test/*",
]

_STORYBOOK = [
    ".storybook/*",
    "src/**/*.stories.ts*",
    "stories/**/*.stories.ts*",
]

SOURCES = glob(
    ["src/**/*.ts*"],
    exclude = _TESTS + _STORYBOOK,
)

TESTS = glob(_TESTS + _STORYBOOK)

STORYBOOK = glob(_STORYBOOK)

# Generates the below helpers.
#
# Usage:
#   bazel run :generate_package_json_helpers
generate_package_json_helpers(
    name = "generate_package_json_helpers",
    local_package_json = ":package.json",
    out_dir = "dist",
    package_json = "//:package.json",
    src_dir = "src",
    workspace = "@npm",
)

### start of auto-generated helpers ###

# Dependencies from package.json
DEPENDENCIES = [
    "@npm//@cognite/cogs.js",
    "@npm//@cognite/sdk",
    "@npm//@types/history",
    "@npm//@cognite/cdf-hub-tenant-selector",
    "@npm//query-string",
    "@npm//axios",
    "@npm//jwt-decode",
    "@npm//lodash",
    "@npm//react",
    "@npm//react-dom",
    "@npm//react-i18next",
    "@npm//react-query",
    "@npm//react-redux",
    "@npm//react-router-dom",
    "@npm//redux",
    "//packages/metrics",
    "//packages/react-i18n",
    "//packages/react-errors",
    "//packages/react-tenant-selector",
    "//packages/sidecar",
    "//packages/intercom-helper",
    "//packages/react-sentry",
    "//packages/auth-utils",
    "//packages/testing",
    "//packages/react-loop-detector",
]

# Type dependencies from package.json
TYPE_DEPENDENCIES = [
    "@npm//@types/axios",
    "@npm//@types/jwt-decode",
    "@npm//@types/lodash",
    "@npm//@types/react",
    "@npm//@types/react-dom",
    "@npm//@types/react-i18next",
    "@npm//@types/react-redux",
    "@npm//@types/react-router-dom",
    "@npm//@types/styled-components",
]

# Dev dependencies from package.json
DEV_DEPENDENCIES = [
    "@npm//@testing-library/jest-dom",
    "@npm//@testing-library/react",
    "@npm//@testing-library/react-hooks",
    "@npm//rollup-plugin-json",
    "@npm//rollup-plugin-typescript2",
    "@npm//typescript",
]

# Mappings for absolute imports from baseUrl @unused
mappings_dict = {
    "Container": "dist/Container",
    "__tests__": "dist/__tests__",
    "auth": "dist/auth",
    "components": "dist/components",
    "hooks": "dist/hooks",
    "index": "dist/index",
    "internal": "dist/internal",
    "mocks": "dist/mocks",
    "providers": "dist/providers",
    "setupTests": "dist/setupTests",
    "utils": "dist/utils",
}

### end of auto-generated helpers ###

eslint_config(
    name = "eslint_config",
    src = ":.eslintrc.js",
    deps = [
        "//:eslint_config",
        "//packages:eslint_config",
    ],
)

eslint_test(
    name = "eslint_test",
    data = SOURCES + TESTS + [
        ":tsconfig.json",
        "//packages:tsconfig.json",
        "//:tsconfig.json",
    ],
    eslint_config = ":eslint_config",
)

prettier_test(
    name = "prettier_test",
    data = SOURCES + TESTS,
    prettier_config = "//:prettier_config",
)

jest_test(
    name = "jest_test",
    srcs = SOURCES + TESTS + [
        ":tsconfig.json",
        "//:tsconfig.json",
        "//packages:jest.config.js",
        "//packages:jest.react.config.js",
        "//packages:tsconfig.json",
    ],
    jest_args = [],
    jest_config = "jest.config.js",
    service_name = "react-container",
    deps = DEPENDENCIES + DEV_DEPENDENCIES + TYPE_DEPENDENCIES,
)

jest(
    name = "jest",
    args = [
        "--node_options=--require=./$(rootpath chdir-workspace.js)",
        "--config",
        "jest.config.js",
        "--runInBand",
        "--watchAll",
    ],
    data = [
        "chdir-workspace.js",
        "jest.config.js",
        "tsconfig.json",
        "//:tsconfig.json",
        "//packages:jest.config.js",
        "//packages:tsconfig.json",
    ],
    tags = [
        # This tag instructs ibazel to pipe into stdin a event describing actions.
        # ibazel send EOF to stdin by default and `react-scripts start` will stop when getting EOF in stdin.
        # So use this to prevent EOF.
        "ibazel_notify_changes",
    ],
)

write_file(
    name = "write_chdir_script",
    out = "chdir.js",
    content = ["process.chdir(__dirname)"],
)

copy_to_bin(
    name = "copy_static_files",
    srcs = SOURCES + [
        ":rollup.config.js",
        ":tsconfig.json",
    ],
)

append_deps(
    name = "package-json",
    dependencies = DEPENDENCIES,
    dev_dependencies = DEV_DEPENDENCIES + TYPE_DEPENDENCIES,
    package = "package.json",
)

rollup(
    name = "dist",
    args = [
        "--banner=//react-container",
        "--node_options=--require=./$(execpath //scripts:log.js)",
        "--node_options=--require=./$(execpath chdir.js)",
        "-c",
        "rollup.config.js",
    ],
    data = DEPENDENCIES + DEV_DEPENDENCIES + TYPE_DEPENDENCIES + [
        ":chdir.js",
        "//scripts:log.js",
        ":copy_static_files",
        ":package-json",
        "//packages:tsconfig",
        "//:tsconfig",
        "@npm//@types/jest",
        "@npm//@types/node",
        "@npm//tslib",
    ],
    output_dir = True,
)

_mappings = module_mappings(
    name = "mappings",
    mappings = mappings_dict,
)

js_library(
    name = "react-container",
    package_name = "@cognite/react-container",
    srcs = ["package-json"],
    visibility = ["//visibility:public"],
    deps = DEPENDENCIES + [":dist"],
)

pkg_npm(
    name = "npm-package",
    package_name = "@cognite/react-container",
    visibility = ["//visibility:public"],
    deps = [
        ":dist",
        "package-json",
    ] + _mappings,
)

is_published(
    name = "is_published",
    package = ":npm-package",
)

package_info(
    name = "package_info",
    display_name = "react-container",
    package = ":npm-package",
)

### Storybook ###

copy_to_bin(
    name = "copy_storybook_files",
    srcs = STORYBOOK,
)

# For ibazel run to properly pick the changes we need to run from the Workspace directory
write_file(
    name = "write_chdir_worspace_script",
    out = "chdir-workspace.js",
    content = ["process.chdir(require('path').join(process.env.BUILD_WORKSPACE_DIRECTORY, '" + package_name() + "'));"],
)

start_storybook(
    name = "start-storybook",
    args = [
        "--node_options=--require=./$(rootpath chdir-workspace.js)",
        "-p",
        "6006",
    ],
    data = DEPENDENCIES + DEV_DEPENDENCIES + TYPE_DEPENDENCIES + [
        "chdir-workspace.js",
        "copy_static_files",
        "copy_storybook_files",
        "@npm//@storybook",
    ],
    tags = [
        # This tag instructs ibazel to pipe into stdin a event describing actions.
        # ibazel send EOF to stdin by default and target will stop when getting EOF in stdin.
        # So use this to prevent EOF.
        "ibazel_notify_changes",
    ],
)
