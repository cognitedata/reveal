load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@npm//@bazel/typescript:index.bzl", "ts_project")
load("@build_bazel_rules_nodejs//:index.bzl", "copy_to_bin", "nodejs_binary")
load("@io_bazel_rules_docker//container:container.bzl", "container_image", "container_layer", "container_push")
load("@npm//@storybook/react:index.bzl", "build_storybook", "start_storybook")
load("@npm//react-scripts:index.bzl", "react_scripts")
load("@npm//testcafe:index.bzl", "testcafe")
load("@npm//cypress:index.bzl", "cypress")
load("@npm//@cognite/fas-tools:index.bzl", "fas_tools")
load("@com_github_cognitedata_bazel_tools//k8s/kubeval:kubeval.bzl", "kubeval_test")
load("//rules:baker_bake.bzl", "baker_bake")
load("//rules/lint:index.bzl", "eslint_config", "eslint_test", "prettier_test")
load("//rules/package_json_helpers:generate.bzl", "generate_package_json_helpers")
load("//rules/publish:publish_fas.bzl", "publish_fas")
load("//rules/publish:publish_storybook.bzl", "publish_storybook")
load("//rules/test:react_scripts_test.bzl", "react_scripts_test")
load("//rules/test:testcafe_test.bzl", "testcafe_test")
load("//rules/test:cypress_batch_test.bzl", "cypress_batch_test")
load("//rules/build_sed:build_sed.bzl", "build_sed")
load("//rules/docker:prepare_docker_build.bzl", "prepare_docker_build")
load("//rules/spinnaker:spinnaker_deployment.bzl", "spinnaker_deployment")

# Filename conventions described at
# https://create-react-app.dev/docs/running-tests#filename-conventions
_TESTS = [
    "src/**/*.test.js*",
    "src/**/*.test.ts*",
    "src/**/*.spec.js*",
    "src/**/*.spec.ts*",
    "src/**/__tests__/**/*.js*",
    "src/**/__tests__/**/*.ts*",
    "src/__mocks__/*",
    "src/**/__mocks__/*",
    "src/utils/test/*",
]

_TESTS_TESTCAFE = [
    "testcafe/**/*.ts",
]

_TESTS_CYPRESS = [
    "cypress/**/*.test.ts",
]

_STORYBOOK = [
    ".storybook/*",
    "src/**/*.stories.ts*",
]

_SOURCES = [
    "src/**/*.ts*",
]

TESTS = glob(_TESTS + _STORYBOOK)

TESTS_TESTCAFE = glob(_TESTS_TESTCAFE)

TESTS_CYPRESS = glob(_TESTS_CYPRESS)

STORYBOOK = glob(_STORYBOOK)

SOURCES = glob(
    _SOURCES,
    exclude = _TESTS + _STORYBOOK,
)

SCRIPTS = glob([
    "scripts/*",
    "scripts/**/*",
])

PUBLIC = glob([
    "public/*",
])

# Generates the below helpers.
#
# Usage:
#   bazel run :generate_package_json_helpers
generate_package_json_helpers(
    name = "generate_package_json_helpers",
    out_dir = "build",
    package_json = "//:package.json",
    src_dir = "src",
    workspace = "@npm",
)

### start of auto-generated helpers ###

# Dependencies from package.json
DEPENDENCIES = [
    "@npm//@cognite/comment-service-types",
    "@npm//@cognite/cogs.js",
    "@npm//@cognite/sdk",
    "@npm//@sentry/browser",
    "@npm//react",
    "@npm//react-dom",
    "@npm//react-i18next",
    "@npm//react-router-dom",
    "@npm//styled-components",
    "//packages/react-container",
    "//packages/react-comments",
    "//packages/react-pid",
    "//packages/react-version",
    "//packages/metrics",
    "//packages/react-i18n",
    "//packages/sidecar",
    "//packages/intercom-helper",
    "//packages/z-index",
]

# Type dependencies from package.json
TYPE_DEPENDENCIES = [
    "@npm//@types/react",
    "@npm//@types/react-dom",
    "@npm//@types/react-i18next",
    "@npm//@types/react-router-dom",
    "@npm//@types/sinon",
    "@npm//@types/styled-components",
]

# Dev dependencies from package.json
DEV_DEPENDENCIES = [
    "@npm//@storybook/addons",
    "@npm//@testing-library/jest-dom",
    "@npm//@testing-library/react",
    "@npm//@testing-library/testcafe",
    "@npm//eslint-plugin-import",
    "@npm//sinon",
]

# Mappings for absolute imports from baseUrl @unused
mappings_dict = {
    "App": "build/App",
    "global-styles": "build/global-styles",
    "index": "build/index",
    "pages": "build/pages",
    "react-app-env.d": "build/react-app-env.d",
    "serviceWorker": "build/serviceWorker",
    "setupTests": "build/setupTests",
    "typings": "build/typings",
    "utils": "build/utils",
}

### end of auto-generated helpers ###

eslint_config(
    name = "eslint_config",
    src = ":.eslintrc.js",
    deps = [
        ".eslintrc.production.js",
        "//:eslint_config",
    ],
)

eslint_test(
    name = "eslint_test",
    data = SOURCES + TESTS + [
        ":tsconfig.json",
        "//:tsconfig.json",
    ],
    eslint_config = ":eslint_config",
)

prettier_test(
    name = "prettier_test",
    data = SOURCES + TESTS,
    prettier_config = "//:prettier_config",
)

# We don't want to teach react-scripts to include from multiple directories
# So we copy everything it wants to read to the output "bin" directory
copy_to_bin(
    name = "copy_static_files",
    srcs = SOURCES + PUBLIC + [
        ":package.json",
        ":tsconfig.json",
    ],
)

# react-scripts can only work if the working directory is the root of the application.
# So we'll need to chdir before it runs.
write_file(
    name = "write_chdir_script",
    out = "chdir.js",
    content = ["process.chdir(__dirname)"],
)

# For ibazel run to properly pick the changes we need to run from the Workspace directory
write_file(
    name = "write_chdir_worspace_script",
    out = "chdir-workspace.js",
    content = ["process.chdir(require('path').join(process.env.BUILD_WORKSPACE_DIRECTORY, '" + package_name() + "'));"],
)

_RUNTIME_DEPS = [
    "chdir.js",
    "copy_static_files",
    "//:tsconfig",
]

### react-scripts build ###

react_scripts(
    # Note: If you want to change the name make sure you update BUILD_PATH below accordingly
    # https://create-react-app.dev/docs/advanced-configuration/
    name = "build",
    args = [
        "--node_options=--require=./$(execpath chdir.js)",
        "build",
    ],
    data = _RUNTIME_DEPS + DEPENDENCIES + TYPE_DEPENDENCIES + [
        # see patches/react-scripts+4.0.3.patch
        # we have added this plugin into webpack so Bazel should know about it
        # TODO: move build script into a custom rules with defined plugin once and for all
        "@npm//tsconfig-paths-webpack-plugin",
    ],
    env = {
        # Specify your Sentry DSN is the URL used to report runtime issues
        "REACT_APP_SENTRY_DSN": "https://da67b4b23d3e4baea6c36de155a08491@sentry.io/3541732",
        # Specify your Mixpanel project token used in users analytics
        "REACT_APP_MIXPANEL_TOKEN": "1cc1cdc82fb93ec9a20a690216de41e4",
        # Specify your locize.io project ID used for localization
        "REACT_APP_LOCIZE_PROJECT_ID": "1ee63b21-27c7-44ad-891f-4bd9af378b72",

        ### DO NOT CHANGE: FAS placeholders
        # In order to build only once we set placeholders into dynamic variables
        # Which will be on FAS publish step in Jenkins replaced to actual values
        # see https://cognitedata.atlassian.net/browse/FAS-159?focusedCommentId=124832
        "PUBLIC_URL": "PUBLIC_URL_VALUE",
        "REACT_APP_APP_ID": "REACT_APP_APP_ID_VALUE",
        "REACT_APP_ENV": "REACT_APP_ENV_VALUE",
        "REACT_APP_RELEASE_ID": "REACT_APP_RELEASE_ID_VALUE",
        "REACT_APP_VERSION_NAME": "REACT_APP_VERSION_NAME_VALUE",
        "REACT_APP_VERSION_SHA": "REACT_APP_VERSION_SHA_VALUE",
        "REACT_APP_VERSION": "$npm_package_version",
        "INLINE_RUNTIME_CHUNK": "false",
        ### END
    },
    output_dir = True,
)

### react-scripts test ###

copy_to_bin(
    name = "copy_test_files",
    srcs = TESTS,
)

react_scripts_test(
    name = "test",
    # folder name to store jest coverage in during CI run
    app_name = "react-demo-app",
    data = _RUNTIME_DEPS + DEPENDENCIES + DEV_DEPENDENCIES + [
        "@npm//@storybook/addon-essentials",
        "@npm//@storybook/addon-links",
        "@npm//@storybook/preset-create-react-app",
        "@npm//@storybook/react",
        "copy_test_files",
    ],
)

### react-scripts start ###

react_scripts(
    name = "start",
    args = [
        "--node_options=--require=./$(rootpath chdir-workspace.js)",
        "start",
    ],
    data = _RUNTIME_DEPS + [
        "chdir-workspace.js",
    ],
    env = {
        "HTTPS": "true",
        "REACT_APP_I18N_DEBUG": "true",
        "REACT_APP_I18N_PSEUDO": "false",
        "REACT_APP_LANGUAGE": "en",
        # Specify your locize.io api key used for localization
        "REACT_APP_LOCIZE_API_KEY": "7a10ae6b-49f3-4600-944d-009140ca6fc9",
        # Specify your locize.io project ID used for localization
        "REACT_APP_LOCIZE_PROJECT_ID": "1ee63b21-27c7-44ad-891f-4bd9af378b72",
        "REACT_APP_LOCIZE_VERSION": "latest",
        "REACT_APP_MIXPANEL_DEBUG": "false",
        # Specify your Mixpanel project token used in users analytics
        "REACT_APP_MIXPANEL_TOKEN": "1cc1cdc82fb93ec9a20a690216de41e4",
        "REACT_APP_VERSION": "$npm_package_version",
        "NODE_OPTIONS": "--max_old_space_size=4096",
    },
    tags = [
        # This tag instructs ibazel to pipe into stdin a event describing actions.
        # ibazel send EOF to stdin by default and `react-scripts start` will stop when getting EOF in stdin.
        # So use this to prevent EOF.
        "ibazel_notify_changes",
    ],
)

### E2E Tests ###

sh_binary(
    name = "build_sed_default",
    srcs = ["//rules/build_sed:sed_linux.sh"],
)

sh_binary(
    name = "build_sed_darwin",
    srcs = ["//rules/build_sed:sed_darwin.sh"],
)

# Replace PUBLIC_URL_VALUE placeholder with '' in order to run testcafe
# If you need additional env vars being set during testcafe run add them into
# :build target above and override here
build_sed(
    name = "build_sed",
    app = "Demo App",
    binary = select({
        "@bazel_tools//src/conditions:darwin": ":build_sed_darwin",
        "//conditions:default": ":build_sed_default",
    }),
    out_dir = "build_bazel",
    src_dir = ":build",
)

### E2E Tests End ###

### Testcafe ###

copy_to_bin(
    name = "copy_testcafe_files",
    srcs = TESTS_TESTCAFE + [
        ".testcaferc.json",
        "tsconfig.testcafe.json",
    ],
)

testcafe_test(
    name = "testcafe_test",
    # folder name to store artifacts during CI run
    app_name = "react-demo-app",
    args = [
        "'chrome --headless --no-sandbox --disable-software-rasterizer'",
        "'testcafe/desktop'",
        "--stop-on-first-fail",
        "--app './scripts/testcafe-bazel-serve.sh'",
        "–q",
        "--config-file .testcaferc.json",
    ],
    data = _RUNTIME_DEPS + DEPENDENCIES + DEV_DEPENDENCIES + TYPE_DEPENDENCIES + SCRIPTS + [
        ":build_sed",
        "copy_testcafe_files",
    ],
    env = {
        "BASE_URL": "http://localhost:11111",
    },
)

testcafe(
    name = "testcafe",
    args = [
        "--node_options=--require=./$(rootpath chdir-workspace.js)",
        "'chrome --disable-software-rasterizer'",
        "'testcafe/desktop'",
        "--stop-on-first-fail",
    ],
    data = _RUNTIME_DEPS + DEPENDENCIES + DEV_DEPENDENCIES + TYPE_DEPENDENCIES + SCRIPTS + [
        ":start",
        "chdir-workspace.js",
        "copy_testcafe_files",
    ],
    env = {
        "BASE_URL": "https://localhost:3000",
    },
)

### Testcafe End ###

### Cypress ###

# The cypress folder has to have a "plugin.ts" file serving your files, as well as a "tsconfig.json"
cypress_batch_test(
    name = "cypress_test",
    build_src = ":build_sed",
    cypress_files = [
        glob(["cypress/*.spec.ts"]),
    ],
    cypress_folder = "cypress",
    global_cypress_files = TESTS_CYPRESS,
    starting_port = 12111,
)

# You can write your cypress tests a javascript files or in typescript.
ts_project(
    name = "cypress_project",
    srcs = TESTS_CYPRESS + glob(["cypress/*.spec.ts"]),
    extends = "cypress/tsconfig.json",
    tsconfig = {
        "compilerOptions": {
            "types": ["cypress"],
        },
    },
    deps = [
        "@npm//cypress",
    ],
)

cypress(
    name = "cypress",
    args = [
        "run",
        "--headed",
        "--project ./apps/react-demo-app",
        "--spec ./apps/react-demo-app/cypress/**/*.spec.js",
        "--env BASE_URL=https://localhost:3000",
    ],
    data = [
        "cypress.json",
        ":cypress_project",
        ":start",
    ],
)

### Cypress End ###

### Storybook ###

copy_to_bin(
    name = "copy_storybook_files",
    srcs = STORYBOOK,
)

build_storybook(
    # Note: If you want to change the name make sure you pass that value with -o into args
    # https://storybook.js.org/docs/react/api/cli-options
    name = "storybook-static",
    args = [
        # Suppress verbose build output
        "--quiet",
    ],
    chdir = "$(RULEDIR)",
    data = _RUNTIME_DEPS + DEPENDENCIES + DEV_DEPENDENCIES + TYPE_DEPENDENCIES + [
        "copy_storybook_files",
        "@npm//tsconfig-paths-webpack-plugin",
        "@npm//@storybook/addon-essentials",
        "@npm//@storybook/addon-links",
        "@npm//@storybook/preset-create-react-app",
        "@npm//@storybook/react",
    ],
    output_dir = True,
)

start_storybook(
    name = "start-storybook",
    args = [
        "--node_options=--require=./$(rootpath chdir-workspace.js)",
        "-p",
        "6006",
    ],
    data = _RUNTIME_DEPS + DEPENDENCIES + DEV_DEPENDENCIES + [
        "chdir-workspace.js",
        "copy_storybook_files",
        "@npm//@types",
        "@npm//@storybook/addon-essentials",
        "@npm//@storybook/addon-links",
        "@npm//@storybook/preset-create-react-app",
    ],
    tags = [
        # This tag instructs ibazel to pipe into stdin a event describing actions.
        # ibazel send EOF to stdin by default and target will stop when getting EOF in stdin.
        # So use this to prevent EOF.
        "ibazel_notify_changes",
    ],
)

publish_storybook(
    name = "publish_storybook",
    build = ":storybook-static",
    # Storybook sub domain to be used
    sub_domain = "react-demo-app",
)

### FAS ###

publish_fas(
    name = "publish_fas",
    build = ":build",
    package_json = ":package.json",
    # Subdomain name used for the pull requests preview.
    preview_subdomain = "react-demo",
    # FAS production app id
    production_app_id = "fas-demo-prod",
    # FAS app identifier (repo) shared across both production and staging apps
    repo_id = "fas-demo",
    # Sentry project name
    sentry_project_name = "react-demo-app",
    # FAS staging app id
    staging_app_id = "fas-demo",
    versioning_strategy = "single-branch",
)

### FAS BUILD ###
fas_tools(
    name = "staging_get_releases",
    args = [
        "get-releases",
        "-d fas-demo",
        "-p $(execpath :config/config-staging.json)",
    ],
    data = [
        ":config/config-staging.json",
        "//apps:google_application_credentials.json",
    ],
    env = {
        "GOOGLE_APPLICATION_CREDENTIALS": "$(execpath //apps:google_application_credentials.json)",
    },
    output_dir = True,
    stdout = "staging_get_releases/releases.csv",
    tags = ["manual"],
)

nodejs_binary(
    name = "staging_prepare_docker_binary",
    data = ["//rules/docker:prepare_docker.mjs"],
    entry_point = "//rules/docker:prepare_docker.mjs",
    env = {
        "GCLOUD_PROJECT": "cognitedata-development",
    },
    tags = ["manual"],
)

prepare_docker_build(
    name = "staging_prepare_docker_build",
    binary = ":staging_prepare_docker_binary",
    configMap = ":config/config-staging.json",
    getRelease = ":staging_get_releases/releases.csv",
    googleCredentials = "//apps:google_application_credentials.json",
    out_dir = "fas-demo",
    tags = ["manual"],
)

container_layer(
    name = "staging_app_layer",
    directory = "/usr/share/caddy",
    files = [":staging_prepare_docker_build"],
    tags = ["manual"],
    visibility = ["//visibility:private"],
)

container_image(
    name = "staging_image",
    base = "//apps:caddy_image",
    entrypoint = [
        "/usr/share/bin/entrypoint.sh",
    ],
    layers = [
        ":staging_app_layer",
    ],
    ports = [
        "8080/tcp",
        "2019/tcp",
    ],
    tags = ["manual"],
    visibility = ["//visibility:private"],
)

container_push(
    name = "push",
    format = "Docker",
    image = ":staging_image",
    registry = "eu.gcr.io",
    repository = "cognitedata/apps/react-demo-app",
    tag = "{BUILD_USER}-{BUILD_TIMESTAMP}",
    tags = ["manual"],
)

spinnaker_deployment(
    name = "spinnaker_deployment",
    baked_manifests = ["//.baker:react-demo-app-manifests"],
    image = ":push",
    service_name = "react-demo-app",
    spinnaker_pipeline = "//spinnaker-config/react-demo-app:spinnaker-config",
    tags = ["manual"],
)

kubeval_test(
    name = "kubeval_test",
    srcs = ["//.baker:react-demo-app-manifests"],
)

baker_bake(
    name = "baker_bake",
    manifests = glob(["kustomize/**/*"]),
    service_name = "react-demo-app",
)
